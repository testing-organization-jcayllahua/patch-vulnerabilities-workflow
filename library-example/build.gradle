buildscript {
    dependencies {
        constraints {
            classpath("org.apache.commons:commons-compress:1.26.0") {
                version {
                    strictly("[1.26.0, 2[")
                    prefer("1.26.0")
                }
                because("CWE-835: Loop with Unreachable Exit Condition ('Infinite Loop')")
            }
        }
    }
}

plugins {
    id 'java'
    id 'java-library'
    id 'maven-publish'
    id 'org.springframework.boot' version '3.4.5'
    id 'io.spring.dependency-management' version '1.1.7'
}

group = 'com.test'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    api 'org.springdoc:springdoc-openapi-starter-webflux-api:2.8.8'
    api 'org.springdoc:springdoc-openapi-starter-webflux-ui:2.8.6'
    api 'org.springframework.boot:spring-boot-starter-webflux'
    api 'org.springframework.security:spring-security-web:6.3.3'
    api 'commons-codec:commons-codec:1.15'
    api 'com.google.guava:guava:33.3.1-jre'
    api 'com.google.code.gson:gson'
    api "org.apache.commons:commons-compress:1.26.0"
    api 'ch.qos.logback.access:common:2.0.3'
    api 'net.logstash.logback:logstash-logback-encoder:8.1'
    api 'org.apache.commons:commons-lang3'
    api 'commons-io:commons-io:2.18.0'
    api 'ch.qos.logback:logback-core:1.1.11'


}

tasks.named('test') {
    useJUnitPlatform()
}

bootJar {
    enabled = false
}

// Enable regular jar task
jar {
    enabled = true
}
publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/testing-organization-jcayllahua/packages")
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from(components.java)
        }
    }
}
